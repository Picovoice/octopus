plugins {
    id 'com.android.application'
}

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
    if (project.hasProperty("pvTestingAccessKey")) {
        properties.put("pvTestingAccessKey", project.getProperty("pvTestingAccessKey"))
    }
    if (project.hasProperty("numTestIterations")) {
        properties.put("numTestIterations", project.getProperty("numTestIterations"))
    }
    if (project.hasProperty("indexPerformanceThresholdSec")) {
        properties.put("indexPerformanceThresholdSec", project.getProperty("indexPerformanceThresholdSec"))
    }
    if (project.hasProperty("searchPerformanceThresholdSec")) {
        properties.put("searchPerformanceThresholdSec", project.getProperty("searchPerformanceThresholdSec"))
    }
}

android {
    compileSdkVersion defaultTargetSdkVersion
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "ai.picovoice.octopusdemo"
        minSdkVersion 21
        targetSdkVersion defaultTargetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue 'string', 'pvTestingAccessKey', properties.getProperty("pvTestingAccessKey", "")
        resValue 'string', 'numTestIterations', properties.getProperty("numTestIterations", "")
        resValue 'string', 'indexPerformanceThresholdSec', properties.getProperty("indexPerformanceThresholdSec", "")
        resValue 'string', 'searchPerformanceThresholdSec', properties.getProperty("searchPerformanceThresholdSec", "")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        androidTest {
            java {
                if (properties.getProperty("indexPerformanceThresholdSec", "").length() == 0 ||
                    properties.getProperty("searchPerformanceThresholdSec", "").length() == 0) {
                    exclude "**/PerformanceTest.java"
                } else {
                    exclude "**/OctopusTest.java"
                }
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'ai.picovoice:octopus-android:1.2.0'

    // Espresso UI Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    androidTestImplementation('com.microsoft.appcenter:espresso-test-extension:1.4')
}
